/*
 * Copyright 2014 Google Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

lpreview = hasProperty('lpreview') ? lpreview.toBoolean() : false;
ext.ioschedSourceBase = lpreview ? 'src/lpreview/' : 'src/classic/';

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion lpreview ? "android-L" : 19
    buildToolsVersion "20.0.0"

    productFlavors { 
        io2014 {
            applicationId = "com.google.samples.apps.iosched"
            uniqueContentIdentifier = "iosched2014"

            manifestPlaceholders = [
                    uniqueContentIdentifier:uniqueContentIdentifier,
                    mapsApiKey:""
            ]

            buildConfigField "boolean", "HAS_VIDEO_LIBRARY", "true"
            buildConfigField "boolean", "SUPPORTS_PEER_BADGE_SCANNING", "true"
            buildConfigField "boolean", "SUPPORTS_REMOTE_VIEWING", "true"

            buildConfigField "double", "VENUE_LATITUDE", "37.783107"
            buildConfigField "double", "VENUE_LONGITUDE", "-122.403789"
            buildConfigField "double", "VENUE_CAMERA_LATITUDE", "37.78308931536713"
            buildConfigField "double", "VENUE_CAMERA_LONGITUDE", "-122.40409433841705"

            buildConfigField "String", "CONFERENCE_DATA_MANIFEST_URL", "\"\""

            buildConfigField "String", "BOOTSTRAP_DATA_TIMESTAMP", "\"Thu, 10 Apr 2014 00:01:03 GMT\""
            buildConfigField "String", "CONFERENCE_HASHTAG", "\"#io14\""
            buildConfigField "String", "CONFERENCE_HASHTAG_PREFIX", "\"#io\""
            buildConfigField "String", "CONFERENCE_TIMEZONE", "\"America/Los_Angeles\""
            buildConfigField "String", "CONFERENCE_GAME_PACKAGE_NAME", "\"com.google.wolff.androidhunt2\""
            buildConfigField "String", "OAUTH_APP_NAME", "\"GoogleIO-Android\""
            buildConfigField "String", "OAUTH_API_KEY", "\"\""
            buildConfigField "String", "UNIQUE_CONTENT_IDENTIFIER", "\""+uniqueContentIdentifier+"\""
            buildConfigField "String", "WIFI_SSID", "\"IO2014\""
            buildConfigField "String", "WIFI_PASSPHRASE", "\"letsdothis\""

            buildConfigField "long[][]", "CONFERENCE_DAYS", "new long[][] {\n" +
                    "            {  " +
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-06-25T07:00:00.000Z\"),\n" +
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-06-26T06:59:59.999Z\")\n" +
                    "            }, {\n" +
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-06-26T07:00:00.000Z\"),\n"+
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-06-27T06:59:59.999Z\")\n"+
                    "            },\n" +
                    "        }"
        }

        droidconuk {
            applicationId = "com.conferenceengineer.android.iosched.event1002"
            uniqueContentIdentifier = "event1002"
            manifestPlaceholders = [
                    uniqueContentIdentifier:uniqueContentIdentifier,
                    mapsApiKey:droidconuk14_android_maps_api_key
            ]

            buildConfigField "boolean", "HAS_VIDEO_LIBRARY", "false"
            buildConfigField "boolean", "SUPPORTS_PEER_BADGE_SCANNING", "false"
            buildConfigField "boolean", "SUPPORTS_REMOTE_VIEWING", "false"

            buildConfigField "double", "VENUE_LATITUDE", "51.535555"
            buildConfigField "double", "VENUE_LONGITUDE", "-0.1047969"
            buildConfigField "double", "VENUE_CAMERA_LATITUDE", "51.535556"
            buildConfigField "double", "VENUE_CAMERA_LONGITUDE", "-0.104797"

            buildConfigField "String", "CONFERENCE_DATA_MANIFEST_URL", "\""+droidconuk14_android_manifest+"\""

            buildConfigField "String", "BOOTSTRAP_DATA_TIMESTAMP", "\"Tue, 2 Sep 2014 14:08:00 GMT\""
            buildConfigField "String", "CONFERENCE_GAME_PACKAGE_NAME", "\"\""
            buildConfigField "String", "CONFERENCE_HASHTAG", "\"#droidconUK\""
            buildConfigField "String", "CONFERENCE_HASHTAG_PREFIX", "\"#dcuk\""
            buildConfigField "String", "CONFERENCE_TIMEZONE", "\"Europe/London\""
            buildConfigField "String", "OAUTH_APP_NAME", "\"DroidconUK2014-2014\""
            buildConfigField "String", "OAUTH_API_KEY", "\""+droidconuk14_android_oauth_api_key+"\""
            buildConfigField "String", "UNIQUE_CONTENT_IDENTIFIER", "\""+uniqueContentIdentifier+"\""
            buildConfigField "String", "WIFI_SSID", "\"DROIDCONUK\""
            buildConfigField "String", "WIFI_PASSPHRASE", "\"lolwaitwaht\""

            buildConfigField "long[][]", "CONFERENCE_DAYS", "new long[][] {\n" +
                    "            {  " +
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-10-30T08:00:00.000Z\"),\n" +
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-10-31T07:59:59.999Z\")\n" +
                    "            }, {\n" +
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-10-31T08:00:00.000Z\"),\n"+
                    "               com.google.samples.apps.iosched.util.ParserUtils.parseTime(\"2014-11-01T07:59:59.999Z\")\n"+
                    "            },\n" +
                    "        }"


        }
    }

    sourceSets {
        main.java.srcDirs = [ 
            ioschedSourceBase + 'java',
            'src/main/java'
        ]
        main.resources.srcDirs = [ 
            ioschedSourceBase + 'res', 
            'src/main/res'
        ] 
    }

    defaultConfig lpreview ? {} : {
        // Non-L-preview configuration
        minSdkVersion 14
        targetSdkVersion 19
    }

    signingConfigs {
        debug {
            storeFile file("../android/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../android/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }
}

dependencies {
    wearApp project(':Wearable')
    compile 'com.google.android.gms:play-services:5.0.89'
    compile 'com.android.support:support-v13:20.+'
    compile 'com.android.support:support-v4:20.+'
    compile 'com.google.android.apps.dashclock:dashclock-api:+'
    compile 'com.google.code.gson:gson:2.+'
    compile('com.google.api-client:google-api-client:1.+') {
        exclude group: 'xpp3', module: 'shared'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'com.google.api-client:google-api-client-android:1.17.+'
    compile 'com.google.apis:google-api-services-plus:+'
    compile 'com.github.japgolly.android:svg-android:2.0.6'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile files('../third_party/glide/library/libs/glide-3.2.0a.jar')
    compile files('../third_party/basic-http-client/libs/basic-http-client-android-0.88.jar')

    // Google Maps Android API utility library
    compile('com.google.maps.android:android-maps-utils:0.3+') {
        exclude group: "com.google.android.gms"
    }

    /* The two dependencies below are only required while we are using http
       Drive appdata. When we start using AppData through GMS,
       they should be removed. */
    compile 'com.google.http-client:google-http-client-gson:+'
    compile 'com.google.apis:google-api-services-drive:+'
}
